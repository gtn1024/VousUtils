plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.vousutils'
version '0.2.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc{
    options.addStringOption("charset", "UTF-8")
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }

            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?:System.getenv("OSSRH_PASSWORD")
            }

        }
    }
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}