plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.vousutils'
version = '0.2.3'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption("charset", "UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'VousUtils'
            from components.java
            pom {
                name = 'VousUtils'
                description = 'A plenty of utils'
                url = 'https://www.vousutils.com'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/gtn1024/VousUtils/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'gtn1024'
                        name = 'Taoning Ge'
                        email = 'tony.bazhang@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/gtn1024/VousUtils.git'
                    developerConnection = 'scm:git:ssh://github.com/gtn1024/VousUtils.git'
                    url = 'https://github.com/gtn1024/VousUtils'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }

            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }

        }
    }
}

signing {
    sign publishing.publications.mavenJava
}